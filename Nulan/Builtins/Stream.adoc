.Stream.nu
[source]
----
(USE {} = "nulan:String")
(USE {} = "nulan:Maybe")
(USE {} = "nulan:Task")

(TYPE (Stream a))

(TYPE (Input a))

(TYPE (Output a))

(EXPORT { make-stream = make_stream
        | with-stream
        | push! = push
        | peek
        | pull! = pull }

  (FFI-IMPORT "FFI/Stream"
    (make_stream :: (-> (-> (Input a) (Task Void))
                        (Stream a)))

    (with_stream :: (-> (Stream a)
                        (-> a b)
                        (-> b)
                        (-> (Output b) (Task c))
                        (Task c)))

    (push :: (-> (Input a) a (Task Void)))

    (peek :: (-> (Output a) (Task a)))

    (pull :: (-> (Output a) (Task a))))

  (FUNCTION
    (with-stream :: (-> (Stream a)
                        (-> (Output (Maybe a)) (Task b))
                        (Task b)))
    (with-stream s f)
      (with_stream s *some *none f)))


(FUNCTION
  (each-output! :: (-> (Output a)
                       (-> a (Task Void))
                       (Task Void)))
  (each-output! in f)
    (LOOP
      (DO-MATCH (pull! in)
        (*none)
          void
        (*some value)
          (DO (f value)
              (loop)))))

(FUNCTION
  (drain-output! :: (-> (Output a) (Task Void)))
  (drain-output! in)
    (LOOP
      (DO-MATCH (pull! in)
        (*none)
          void
        (*some _)
          (loop))))

(FUNCTION
  (push-drain! :: (-> (Output a) (Input b) b (Task Void)))
  (push-drain! in out value)
    # TODO when `drain-output!` gets killed, does it *immediately* stop,
    #      or is there the possibility that it will consume 1 extra
    #      value from the Stream ?
    (fastest
      (push! out value)
      (DO (drain-output! in)
          never)))

(FUNCTION
  (drain :: (-> (Stream a) (Task Void)))
  (drain in)
    (with-stream in drain-output!))

# TODO rename to stream-each ?
(FUNCTION
  (each :: (-> (Stream a)
               (-> a (Task Void))
               (Task Void)))
  (each in f)
    (with-stream in -> in
      (each-output! in f)))

(FUNCTION
  (throttle :: (-> (Stream a) Integer (Stream a)))
  (throttle in ms)
    (make-stream -> out
      (with-stream in -> in
        (each-output! in -> value
          (fastest
            # TODO if the input Stream is finished, it will still wait `ms`
            #      milliseconds before closing the output Stream
            (ignore-concurrent
              (push! out value)
              (delay ms))
            (DO (drain-output! in)
                never))))))

(IMPLEMENT (Stream a)
  (empty)
    (make-stream -> _ void)

  (map in f)
    (make-stream -> out
      (each in -> value
        (push! out (f value))))

  (++ @s)
    (make-stream -> out
      (ignore-sequential
        @(map s -> s
           (each s -> value
             (push! out value))))))

(FUNCTION
  (merge :: (-> @(Stream a) (Stream a)))
  (merge @s)
    (make-stream -> out
      (ignore-concurrent
        @(map s -> s
           (each s -> value
             (push! out value))))))

# TODO make this generic ?
(FUNCTION
  (keep :: (-> (Stream a)
               (-> a Boolean)
               (Stream a)))
  (keep in f)
    (make-stream -> out
      (each in -> value
        (IF (f value)
          (push! out value)
          void))))

# TODO make this generic ?
(FUNCTION
  (stream-foldl :: (-> (Stream a)
                       b
                       # TODO maybe the function should return `b` rather than `(Task b)` ?
                       (-> b a (Task b))
                       (Task b)))
  (stream-foldl s init f)
    (with-stream s -> in
      (LOOP old = init
        (DO-MATCH (pull! in)
          (*none)
            (wrap old)
          (*some value)
            (DO new = (f old value)
                (loop new))))))

# TODO make this generic ?
(FUNCTION
  (stream-length :: (-> (Stream a) (Task Integer)))
  (stream-length s)
    (stream-foldl s 0 -> old _
      (wrap (+ old 1))))

# TODO maybe convert the Stream into a List, and then use (++ @list) ?
(FUNCTION
  # TODO is this correct ?
  (stream-join :: (-> (Stream a) (Task a)))
  (stream-join in)
    (stream-foldl in (empty) -> old new
      (wrap (++ old new))))

# TODO make this generic ?
(FUNCTION
  # TODO this type signature is probably wrong
  (flatten :: (-> (Stream a) (Stream b)))
  (flatten in)
    (make-stream -> out
      (each in -> value
        (ignore-sequential
          @(map value -> value
             (push! out value))))))

# TODO this is probably incorrect
(FUNCTION
  (split-lines :: (-> (Stream String) (Stream String)))
  (split-lines in)
    (<< (map in -> s (split s "\n"))
        (flatten)))

(FUNCTION
  (generate :: (-> a (-> a a) (Stream a)))
  (generate init f)
    (make-stream -> out
      (LOOP x = init
        (DO (push! out x)
            (loop (f x))))))
----

.Examples
[source]
----
(FUNCTION
  (generate-add :: (-> Integer (Stream Integer)))
  (generate-add init inc)
    (generate init -> x (add x inc)))

(FUNCTION
  (generate-multiply :: (-> Integer (Stream Integer)))
  (generate-multiply init inc)
    (generate init -> x (multiply x inc)))

(FUNCTION
  (accumulate :: (-> (Stream Integer) (Task Integer)))
  (accumulate in)
    (stream-foldl in 0 -> old value
      (LET new = (add old value)
        (DO (log! new)
            (wrap new)))))


# More verbose version with comments
(FUNCTION (main)
  (LET # Lazily generates the stream [0 1 2 3 4 ...]
       x = (generate-add 0 1)

       # Lazily generates the stream [1 2 4 8 16 ...]
       y = (generate-multiply 1 2)

       # Merges the two streams in a non-deterministic fashion
       z = (merge x y)

    # Accumulates and logs the sum of the merged stream
    (accumulate z)))


# More concise version
(FUNCTION (main)
  (accumulate (merge (generate-add 0 1)
                     (generate-multiply 1 2))))
----
