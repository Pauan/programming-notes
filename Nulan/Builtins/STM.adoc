.STM.nu
[source]
----
(USE {} = "nulan:Task")
(USE {} = "nulan:Monad")

(TYPE (STM a))

(TYPE (Ref a))

# TODO should it be polymorphic on `r` ?
(INTERFACE (%Ref t r)
  (make-ref :: (-> a (t (r a))))
  (ref-get :: (-> (r a) (t a))))

(EXPORT { atomic
        | retry
        | or
        | ref-set! = stm_ref_set }

  (FFI-IMPORT "FFI/STM"
    (_wrap :: (-> a (STM a)))

    (_bind :: (-> (STM a) (-> a (STM b)) (STM b)))

    (_error :: (-> String (STM a)))

    (atomic :: (-> (STM a) (Task a)))

    (retry :: (-> (STM a)))

    (or :: (-> @(STM a) (STM a)))

    (task_make_ref :: (-> a (Task (Ref a))))

    (task_ref_get :: (-> (Ref a) (STM a)))

    (stm_make_ref :: (-> a (STM (Ref a))))

    (stm_ref_get :: (-> (Ref a) (STM a)))

    (stm_ref_set :: (-> (Ref a) a (STM Void))))


  (IMPLEMENT (%Ref (Task a) (Ref a))
    (make-ref a)
      (task_make_ref a)

    (ref-get r)
      (task_ref_get r))


  (IMPLEMENT (%Ref (STM a) (Ref a))
    (make-ref a)
      (stm_make_ref a)

    (ref-get r)
      (stm_ref_get r))


  (IMPLEMENT (%Error (STM a))
    (error s)
      (_error s))


  (IMPLEMENT (%Monad (STM a))
    (wrap x)
      (_wrap x)

    (bind x f)
      (_bind x f)))


(TYPE (MRef a)
  (*mref (Ref (Maybe a))))

(FUNCTION
  (make-mref :: (-> (MRef a)))
  (make-mref)
    (*mref (make-ref (*none))))

(FUNCTION
  (take :: (-> (MRef a) (STM a)))
  (take (*mref ref))
    (DO-MATCH (ref-get ref)
      (*none)
        (retry)
      (*some value)
        (DO (ref-set! ref (*none))
            (wrap value))))

(FUNCTION
  (put! :: (-> (MRef a) a (STM Void)))
  (put! (*mref ref) value)
    (DO-MATCH (ref-get ref)
      (*none)
        (ref-set! ref (*some value))
      (*some _)
        (retry)))
----

.Examples
[source]
----
(ALIAS Cent = Integer)

(TYPE Account
  (*account { balance = (Ref Cent) }))

(FUNCTION
  (make-account :: (IMPLEMENT (%Ref a) (%Monad a)
                     (-> Cent (a Account))))
  (make-account balance)
    (DO balance = (make-ref balance)
        (wrap (*account { balance }))))

(FUNCTION
  (balance :: (IMPLEMENT (%Ref a)
                (-> Account (a Cent))))
  (balance (*account { balance }))
    (ref-get balance))

(FUNCTION
  (deposit! :: (-> Account Cent (STM Void)))
  (deposit! (*account { balance }) amount)
    (DO x = (ref-get balance)
        (ref-set! balance (+ x amount))))

(FUNCTION
  (withdraw! :: (-> Account Cent (STM Void)))
  (withdraw! (*account { balance }) amount)
    (DO x = (ref-get balance)
        (IF (more? amount balance)
          (retry)
          (ref-set! balance (- x amount)))))

(FUNCTION
  (transfer! :: (-> Account Account Cent (STM Void)))
  (transfer! from to amount)
    (DO (withdraw! from amount)
        (deposit! to amount)))

 #\
 # Creates two accounts, one with 5 cents, the other with 10 cents.
 #
 # It then concurrently attempts to transfer 15 cents from `x` to `y`,
 # and also attempts to transfer 10 cents from `y` to `x`.
 #
 # Because `x` does not have enough money, the transfer waits until `x` has
 # sufficient funds.
 #
 # The account `y` does have enough money, and so 10 cents is transferred to
 # `x`. `x` now has 15 cents. This causes the transfer from `x` to `y` to be
 # retried. This time `x` has enough money, so it transfers 15 cents to `y`.
 #
 # The final result is that `x` has 0 cents, and `y` has 15 cents. This
 # happens regardless of what order the transfers occur, and each transfer
 # happens atomically.
\#
(DO x = (make-account 5)
    y = (make-account 10)

  (ignore-concurrent
    (atomic (transfer! x y 15))
    (atomic (transfer! y x 10)))

  (log! (balance x))
  (log! (balance y)))
----
